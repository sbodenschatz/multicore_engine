cmake_minimum_required (VERSION 3.10)

include(SourceGroupGenerator)
make_src_groups_code("include/mce" "src")

include_directories(${PROJECT_SOURCE_DIR}/../multicore_engine_tests/include)

set(TEST_BINARY_MODE "GROUPS" CACHE STRING "Granularity of test binaries (FULL,GROUPS,SINGLE)")

if(TEST_BINARY_MODE STREQUAL "FULL")
	file(GLOB_RECURSE CORE_TESTS_SRC "src/*.cpp")
	add_executable(mce_tests ${CORE_TESTS_SRC})
	target_compile_definitions(mce_tests PRIVATE $<$<CXX_COMPILER_ID:Clang>: MCECLANG>)
	if(NOT STATIC_ANALYSIS_ONLY)
		add_dependencies(mce_tests mce_assets_packs)
	endif()
	make_src_groups()
	target_link_libraries(mce_tests mce_core mce_renderer GTest::GTest GTest::Main)
	enable_custom_lto(mce_tests)
	add_test(NAME mce_tests COMMAND mce_tests)
elseif(TEST_BINARY_MODE STREQUAL "GROUPS")
	file(GLOB TESTS_SRC_DIRS RELATIVE ${CMAKE_CURRENT_SOURCE_DIR}/src "src/*")
	foreach(TESTS_SRC_DIR ${TESTS_SRC_DIRS})
		if(IS_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/src/${TESTS_SRC_DIR})
			file(GLOB TESTS_SRCS "src/${TESTS_SRC_DIR}/*.cpp")
			set(TARGET_NAME mce_tests_${TESTS_SRC_DIR})
			add_executable(${TARGET_NAME} ${TESTS_SRCS}
					$<$<STREQUAL:"${TESTS_SRC_DIR}","graphics">:src/vk_mock_interface.cpp>)
			target_compile_definitions(${TARGET_NAME} PRIVATE $<$<CXX_COMPILER_ID:Clang>: MCECLANG>)
			if(NOT STATIC_ANALYSIS_ONLY)
				add_dependencies(${TARGET_NAME} mce_assets_packs)
			endif()
			target_link_libraries(${TARGET_NAME} mce_core mce_renderer GTest::GTest GTest::Main)
			enable_custom_lto(${TARGET_NAME})
			add_test(NAME ${TESTS_SRC_DIR} COMMAND ${TARGET_NAME})
		endif()
	endforeach()
else()
	file(GLOB TESTS_SRC_DIRS RELATIVE ${CMAKE_CURRENT_SOURCE_DIR}/src "src/*")
	foreach(TESTS_SRC_DIR ${TESTS_SRC_DIRS})
		if(IS_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/src/${TESTS_SRC_DIR})
			file(GLOB TESTS_SRCS RELATIVE ${CMAKE_CURRENT_SOURCE_DIR}/src/${TESTS_SRC_DIR} "src/${TESTS_SRC_DIR}/*.cpp")
			foreach(TESTS_SRC ${TESTS_SRCS})
				string(REPLACE ".cpp" "" TESTS_SRC_NAME ${TESTS_SRC})
				set(TARGET_NAME mce_test_${TESTS_SRC_DIR}_${TESTS_SRC_NAME})
				add_executable(${TARGET_NAME} ${CMAKE_CURRENT_SOURCE_DIR}/src/${TESTS_SRC_DIR}/${TESTS_SRC}
						$<$<STREQUAL:"${TESTS_SRC_DIR}","graphics">:src/vk_mock_interface.cpp>)
				target_compile_definitions(${TARGET_NAME} PRIVATE $<$<CXX_COMPILER_ID:Clang>: MCECLANG>)
				if(NOT STATIC_ANALYSIS_ONLY)
					add_dependencies(${TARGET_NAME} mce_assets_packs)
				endif()
				target_link_libraries(${TARGET_NAME} mce_core mce_renderer GTest::GTest GTest::Main)
				enable_custom_lto(${TARGET_NAME})
				add_test(NAME ${TESTS_SRC_DIR}_${TESTS_SRC_NAME} COMMAND ${TARGET_NAME} )
			endforeach()
		endif()
	endforeach()
endif()
