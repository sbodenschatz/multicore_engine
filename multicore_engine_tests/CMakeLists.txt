cmake_minimum_required (VERSION 3.7)

project(multicore_engine_tests)

make_src_groups()

include_directories(${CMAKE_SOURCE_DIR}/multicore_engine_tests/include)

set(TEST_BINARY_MODE "GROUPS" CACHE STRING "Granularity of test binaries (FULL,GROUPS,SINGLE)")

if(TEST_BINARY_MODE STREQUAL "FULL")
	file(GLOB_RECURSE CORE_TESTS_SRC "src/*.cpp")
	add_executable(mce_tests ${CORE_TESTS_SRC})
	if(NOT STATIC_ANALYSIS_ONLY)
		add_dependencies(mce_tests mce_assets_packs)
	endif()
	make_src_groups()
	target_link_libraries(mce_tests mce_core mce_renderer deps.gtest)
	if(MSVC)
		set_target_properties(mce_tests PROPERTIES LINK_FLAGS_RELEASE "/LTCG")
		set_target_properties(mce_tests PROPERTIES LINK_FLAGS_MINSIZEREL "/LTCG")
		set_target_properties(mce_tests PROPERTIES LINK_FLAGS_RELWITHDEBINFO "/LTCG")
	endif()
	add_test(NAME mce_tests COMMAND mce_tests)
elseif(TEST_BINARY_MODE STREQUAL "GROUPS")
	file(GLOB TESTS_SRC_DIRS RELATIVE ${CMAKE_CURRENT_SOURCE_DIR}/src "src/*")
	foreach(TESTS_SRC_DIR ${TESTS_SRC_DIRS})
		if(IS_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/src/${TESTS_SRC_DIR})
			file(GLOB TESTS_SRCS "src/${TESTS_SRC_DIR}/*.cpp")
			set(TARGET_NAME mce_tests_${TESTS_SRC_DIR})
			add_executable(${TARGET_NAME} ${TESTS_SRCS})
			if(NOT STATIC_ANALYSIS_ONLY)
				add_dependencies(${TARGET_NAME} mce_assets_packs)
			endif()
			target_link_libraries(${TARGET_NAME} mce_core mce_renderer deps.gtest)
			if(MSVC)
				set_target_properties(${TARGET_NAME} PROPERTIES LINK_FLAGS_RELEASE "/LTCG")
				set_target_properties(${TARGET_NAME} PROPERTIES LINK_FLAGS_MINSIZEREL "/LTCG")
				set_target_properties(${TARGET_NAME} PROPERTIES LINK_FLAGS_RELWITHDEBINFO "/LTCG")
			endif()
			add_test(NAME ${TESTS_SRC_DIR} COMMAND ${TARGET_NAME})
		endif()
	endforeach()
else()
	file(GLOB TESTS_SRC_DIRS RELATIVE ${CMAKE_CURRENT_SOURCE_DIR}/src "src/*")
	foreach(TESTS_SRC_DIR ${TESTS_SRC_DIRS})
		if(IS_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/src/${TESTS_SRC_DIR})
			file(GLOB TESTS_SRCS RELATIVE ${CMAKE_CURRENT_SOURCE_DIR}/src/${TESTS_SRC_DIR} "src/${TESTS_SRC_DIR}/*.cpp")
			foreach(TESTS_SRC ${TESTS_SRCS})
				string(REPLACE ".cpp" "" TESTS_SRC_NAME ${TESTS_SRC})
				set(TARGET_NAME mce_test_${TESTS_SRC_DIR}_${TESTS_SRC_NAME})
				add_executable(${TARGET_NAME} ${CMAKE_CURRENT_SOURCE_DIR}/src/${TESTS_SRC_DIR}/${TESTS_SRC})
				if(NOT STATIC_ANALYSIS_ONLY)
					add_dependencies(${TARGET_NAME} mce_assets_packs)
				endif()
				target_link_libraries(${TARGET_NAME} mce_core mce_renderer deps.gtest)
				if(MSVC)
					set_target_properties(${TARGET_NAME} PROPERTIES LINK_FLAGS_RELEASE "/LTCG")
					set_target_properties(${TARGET_NAME} PROPERTIES LINK_FLAGS_MINSIZEREL "/LTCG")
					set_target_properties(${TARGET_NAME} PROPERTIES LINK_FLAGS_RELWITHDEBINFO "/LTCG")
				endif()
				add_test(NAME ${TESTS_SRC_DIR}_${TESTS_SRC_NAME} COMMAND ${TARGET_NAME} )
			endforeach()
		endif()
	endforeach()
endif()